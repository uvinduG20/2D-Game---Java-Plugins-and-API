plugins {
    id "java"
    id "application"
    id "pmd"
    id "org.openjfx.javafxplugin" version "0.1.0"
    id 'com.intershop.gradle.javacc' version '4.1.3'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "net.java.dev.javacc:javacc:7.0.5"
    implementation "com.google.guava:guava:31.1-jre"
    implementation 'org.python:jython-standalone:2.7.2'
    implementation project(':API')
    implementation project(':plugins')


}

javacc {
    configs {
        createparser {
            inputFile = file('src/main/jj/GameConfigParser.jj')
            packageName = 'edu.curtin.saed.assignment1'
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated/javacc/createparser']
        }
    }
}

application {
    mainClass = "edu.curtin.saed.assignment1.App"
}

javafx {
    version = '21'
    modules = [ 'javafx.controls' ]
}

pmd {
    consoleOutput = true
    toolVersion = "7.3.0"
    rulesMinimumPriority = 5
    ruleSetFiles = files("saed-pmd-rules.xml")
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all,-serial"
}

tasks.named("run") {
    environment["PATH"] = (
            layout.projectDirectory.dir("comms").dir("bin").toString()
                    + File.pathSeparator
                    + System.getenv("PATH"))

    // Enable passing command-line arguments
    args = project.hasProperty("appArgs") ? project.appArgs.split(',') : []
}